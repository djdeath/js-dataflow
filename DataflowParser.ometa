let DataflowParser = ometa(BaseStrParser) {
  space        = ^space | fromToOrEnd('//', '\n'),

  nonEmptyListOf :r :d = apply(r):f (apply(d) apply(r))*:rs  -> [f].concat(rs),

  // Strings:
  str          = spaces '\'' (~'\'' char)*:cs '\''           -> cs.join('')
               | spaces '"'  (~'"'  char)*:cs '"'            -> cs.join(''),

  // Numbers:
  number       = spaces <digit+ ('.' digit+)?>:f             -> parseFloat(f),

  // Identifiers:
  nameFirst    = letter | '$' | '_',
  nameRest     = nameFirst | digit,
  name         = spaces <nameFirst nameRest*>,

  // Builtins:
  builtin      = "@" name,

  // Expressions:
  expr         = expr:x "||" andExpr:y                                    -> [#binop, "||", x, y]
               | andExpr,
  andExpr      = andExpr:x "&&" eqExpr:y                                  -> [#binop, "&&", x, y]
               | eqExpr,
  eqExpr       = eqExpr:x ( "=="  relExpr:y                               -> [#binop, "==",  x, y]
                          | "!="  relExpr:y                               -> [#binop, "!=",  x, y]
                          | "===" relExpr:y                               -> [#binop, "===", x, y]
                          | "!==" relExpr:y                               -> [#binop, "!==", x, y]
                          )
               | relExpr,
  relExpr      = relExpr:x ( ">"          addExpr:y                       -> [#binop, ">",   x, y]
                           | ">="         addExpr:y                       -> [#binop, ">=",  x, y]
                           | "<"          addExpr:y                       -> [#binop, "<",   x, y]
                           | "<="         addExpr:y                       -> [#binop, "<=",  x, y]
                           )
               | addExpr,
  addExpr     = addExpr:x "+" mulExpr:y                                   -> [#binop, "+",   x, y]
               | addExpr:x "-" mulExpr:y                                  -> [#binop, "-",   x, y]
               | mulExpr,
  mulExpr      = mulExpr:x "*" unary:y                                    -> [#binop, "*",   x, y]
               | mulExpr:x "/" unary:y                                    -> [#binop, "/",   x, y]
               | mulExpr:x "%" unary:y                                    -> [#binop, "%",   x, y]
               | unary,
  unary        = "-"      primExpr:p                                      -> [#unop,  "-",   p]
               | "+"      primExpr:p                                      -> [#unop,  "+",   p]
               | primExpr,
  primExpr     = primExpr:p ( "[" expr:i "]"                              -> [#getp, i, p]
                            | "." name:m "(" listOf(#expr, ','):as ")"    -> [#send, m, p].concat(as)
                            | "." name:f                                  -> [#getp, [#string, f], p]
                            | "(" listOf(#expr, ','):as ")"               -> [#call, p].concat(as)
                            )
               | builtin:b "(" listOf(#expr, ','):as ")"                  -> [#builtin, b, this._uniqueId(b)].concat(as)
               | primExprHd,
  primExprHd   = "(" expr:e ")"                                           -> e
               | name:n                                                   -> [#get, n]
               | number:n                                                 -> [#number, n]
               | str:s                                                    -> [#string, s]
               | "[" enum(#expr, ','):es "]"                              -> [#arr].concat(es),

  condExpr     = "when" expr:c "->" expr:e                                -> [#when, c, e],
  condExprs    = nonEmptyListOf(#condExpr, #space):cs "else" expr:e       -> [#switch].concat(cs).concat([[#else, e]])
               | nonEmptyListOf(#condExpr, #space):cs                     -> [#switch].concat(cs)
               | expr,

  // Propagations:
  propagate    = name:n "<-" condExprs:e                                  -> [#propagate, n, e, @location],

  grammar      = spaces (propagate:p spaces -> p)*:pp end                 -> [#dataflow].concat(pp),
};
DataflowParser._uniqueId = function(base) {
  if (this._id === undefined)
    this._id = 0;
  this._id += 1;
  return base + ':' + this._id;
};
